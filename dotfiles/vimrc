" vim: set foldmethod=marker foldlevel=0:
" ============================================================================
" " VUNDLE BLOCK {{{
" ============================================================================
set nocompatible              " be iMproved, required
filetype off                  " required

" set the runtime path to include Vundle and initialize
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()
" alternatively, pass a path where Vundle should install plugins
"call vundle#begin('~/some/path/here')

" let Vundle manage Vundle, required
Plugin 'VundleVim/Vundle.vim'

" Plugin 'vim-scripts/matchit.zip'
Plugin 'flazz/vim-colorschemes'
Plugin 'tpope/vim-sensible'
Plugin 'tpope/vim-surround'
Plugin 'tpope/vim-repeat'
Plugin 'tpope/vim-commentary'
Plugin 'tpope/vim-eunuch'
Plugin 'tpope/vim-unimpaired'
Plugin 'tommcdo/vim-exchange'
" Plugin 'justinmk/vim-dirvish'
Plugin 'rafi/vim-tinyline'
" Plugin 'kshenoy/vim-signature'

" Git
Plugin 'tpope/vim-fugitive'
Plugin 'junegunn/gv.vim'

Plugin 'junegunn/vim-peekaboo'
Plugin 'junegunn/rainbow_parentheses.vim'
Plugin 'vbauerster/vim-highlighter'

" Tmux
" Plugin 'christoomey/vim-tmux-navigator'

" All of your Plugins must be added before the following line
call vundle#end()            " required

" }}}
" ============================================================================
" Script local functions {{{
" ============================================================================

function! s:isBufferOpen(bufname)
  redir =>buflist
  silent! ls!
  redir END
  for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
    if bufwinnr(bufnum) != -1
      return 1
    endif
  endfor
endfunction

" http://vim.wikia.com/wiki/Toggle_to_open_or_close_the_quickfix_window
function! s:toggleList(bufname, pfx)
  if <sid>isBufferOpen(a:bufname)
    exec(a:pfx.'close')
    return
  endif
  if a:pfx == 'l' && len(getloclist(0)) == 0
      echohl ErrorMsg
      echo "Location List is Empty."
      return
  endif
  let winnr = winnr()
  exec(a:pfx.'open')
  if winnr() != winnr
    wincmd p
  endif
endfunction

function! s:scrolList(dir)
  if empty(&buftype)
    if <sid>isBufferOpen("Quickfix List")
      return (a:dir ==# "cn" ? ":cnext" : ":cNext")."\<CR>"
    endif
    if <sid>isBufferOpen("Location List")
      return (a:dir ==# "cn" ? ":lnext" : ":lNext")."\<CR>"
    endif
  endif
  return a:dir ==# "cn" ? "\<C-n>" : "\<C-p>"
endfunction

function! s:helptab()
  if &buftype ==# "help"
    wincmd T
    nnoremap <buffer> q :q<CR>
  endif
endfunction

" vim-vertical-move replacement
" credit: cherryberryterry: https://www.reddit.com/r/vim/comments/4j4duz/a/d33s213
function! s:vjump(dir)
  let c = '%'.virtcol('.').'v'
  let flags = a:dir ? 'bnW' : 'nW'
  let bot = search('\v'.c.'.*\n^(.*'.c.'.)@!.*$', flags)
  let top = search('\v^(.*'.c.'.)@!.*$\n.*\zs'.c, flags)
  echom string(bot) string(top)

  " norm! m`
  return a:dir ? (line('.') - (bot > top ? bot : top)).'k'
    \        : ((bot < top ? bot : top) - line('.')).'j'
endfunction

" }}}
" ============================================================================
" BASIC SETTINGS {{{
" ============================================================================

let mapleader = ' '
let maplocalleader = ' '

set background=dark
colorscheme lxvc

set ttyfast
set encoding=utf-8
" set pastetoggle=<F3>
set history=1000               " nvim sets this to 1000 by default
set undolevels=1000            " nvim sets this to 1000 by default
set autoread                   " detect when a file is changed
set hlsearch                   " Highlight search terms

" Excluding version control directories
set wildignore+=*/.git/*,*/.hg/*,*/.svn/*
" Binary images
set wildignore+=*.jpg,*.bmp,*.gif,*.png,*.jpeg

set number
set wildmode=list:longest            " TAB auto-completion for file paths
set hidden                           " current buffer can be put into background
set showcmd                          " show incomplete commands
set scrolloff=2                      " lines of text around cursor
set foldlevelstart=99                " all folds open by default
set cmdheight=1                      " command bar height
set noerrorbells
set complete=.,w,t
set completeopt=menu,longest


" SPACES & TABS
" Explanations from http://tedlogan.com/techblog3.html
set tabstop=2     " How many columns a tab counts for
set softtabstop=2 " How many columns vim uses when pressing TAB in insert mode
set shiftwidth=2  " How many columns text is indented with << and >>
set expandtab     " Use spaces
set smartindent   " Normally 'autoindent' should also be on when using 'smartindent'
set nojoinspaces  " Prevents inserting two spaces after punctuation on a join (J)

" Searching
set gdefault   " global search by default
set ignorecase " case insensitive searching
set smartcase  " case-sensitive if expresson contains a capital letter
set lazyredraw " don't redraw while executing macros

" Highlight current line
" http://stackoverflow.com/questions/8247243/highlighting-the-current-line-number-in-vim
set cursorline

" Open new split panes to right and bottom, which feels more natural
" https://robots.thoughtbot.com/vim-splits-move-faster-and-more-naturally
set splitbelow
set splitright

" Always use vertical diffs
set diffopt+=vertical

" Whitespaces
set listchars=tab:»⋅,trail:⋅,nbsp:⋅,extends:❯,precedes:❮

" backup/persistance settings
set noswapfile

" }}}
" ============================================================================
" MAPPINGS {{{
" ============================================================================
" open vimrc
nnoremap <leader>ev <C-w><C-v><C-l>:e $MYVIMRC<cr>
" edit tmux.conf
nnoremap <leader>et :e ~/.tmux.conf<CR>
" edit logs
nnoremap <leader>em :e /var/log/messages<CR>
nnoremap <leader>eu :e /opt/etc/runblock/runblock.dnsmasq<CR>

" set working directory to the current buffer's directory
nnoremap cd :lcd %:p:h<bar>pwd<cr>
nnoremap cu :lcd ..<bar>pwd<cr>
nnoremap cD :cd %:p:h<bar>pwd<cr>
nnoremap cU :cd ..<bar>pwd<cr>

" In normal mode, we use : much more often than ; so lets swap them.
" WARNING: this will cause any "ordinary" map command without the "nore" prefix
" that uses ":" to fail. For instance, "map <f2> :w" would fail, since vim will
" read ":w" as ";w" because of the below remappings. Use "noremap"s in such
" situations and you'll be fine.
noremap ; :

" Swap implementations of ` and ' jump to markers
nnoremap ' `
nnoremap ` '

" quickly access yank reg
noremap "" "0

nmap <Enter> %

" F1 will search help for the word under the cursor
nnoremap <F1> :help <C-r><C-w><CR>

" tab shortcuts
nnoremap g{ gT
nnoremap g} gt
nnoremap <Leader>tn :tabnew<CR>
nnoremap <Leader>tx :tabclose<CR>

" Save
nnoremap <Leader>w :update<CR>

" Quit nvim
nnoremap <silent> <F10> :qa<CR>
nnoremap <silent> <F22> :qa!<CR>
" Quit/close window
nnoremap <Leader>q :q<CR>
nnoremap <Leader>! :q!<CR>
nnoremap <silent> <Leader>x :close<CR>

" Read :help ctrl-w_w
" Read :help wincmd
nnoremap <Tab> <C-w>w
nnoremap <S-Tab> <C-w>W
" Go to previous (last accessed) window
" nnoremap <Leader><Tab> <C-w>p

" make Y consistent with C and D. See :help Y.
nnoremap Y y$

" <C-w> c Close the current window
" <C-w> z Close any "Preview" window currently open
" <C-w> P Go to preview window

" Toggle to open or close the quickfix window
" http://vim.wikia.com/wiki/Toggle_to_open_or_close_the_quickfix_window
" http://stackoverflow.com/questions/13208660/how-to-enable-mapping-only-if-there-is-no-quickfix-window-opened-in-vim
nmap <silent> <leader>ll :call <sid>toggleList("Location List", 'l')<CR>
nmap <silent> <leader>cc :call <sid>toggleList("Quickfix List", 'c')<CR>

nnoremap <expr><C-n> <sid>scrolList("cn")
nnoremap <expr><C-p> <sid>scrolList("cp")

" Select blocks after indenting
xnoremap < <gv
xnoremap > >gv|

" Use tab for indenting in visual mode
xnoremap <Tab> >gv|
xnoremap <S-Tab> <gv
nnoremap > >>_
nnoremap < <<_

" reselect last paste
nnoremap gp `[v`]

" Search in normal mode with very magic on
nnoremap / /\v
nnoremap ? ?\v
" Search in visually selected block only
vnoremap / <Esc>/\%V\v
vnoremap ? <Esc>?\%V\v

vmap <leader>s :sort<CR>

" moving up and down work as you would expect
nnoremap <silent> j gj
nnoremap <silent> k gk
nnoremap <silent> ^ g^
nnoremap <silent> $ g$

noremap <silent> + 4gj
noremap <silent> - 4gk

" vim-vertical-move
noremap <expr> gj <SID>vjump(0)
noremap <expr> gk <SID>vjump(1)

" auto center
nnoremap <silent> n nzz
nnoremap <silent> N Nzz
nnoremap <silent> * *zz
nnoremap <silent> # #zz
nnoremap <silent> g* g*zz
nnoremap <silent> g# g#zz

" fold a html tag
nnoremap <leader>ft Vatzf

" Read :help g_ctrl-]
" same as :tjump
" jump to tag if there's only one matching tag, but show list of
" options when there is more than one definition
nnoremap <c-]> g<c-]>

" Remove spaces at the end of lines
nnoremap <silent><Leader>w<Leader> :<C-u>silent! keeppatterns %substitute/\s\+$//e<CR>

" Buffer reload
nnoremap <Leader>rr :e!<CR>

" switch between buffers
" bprev provided by unimpaired [b
" bnext provided by unimpaired ]b
nnoremap <silent><Leader>. <C-^><CR>
nnoremap <Leader>bx :bd<CR>

nnoremap <Leader>v <C-w>v

" Show Registers
" nnoremap <leader>di :di<CR>

" Highlight All
nnoremap <Leader>ha :Highlight<CR>
nnoremap <silent><C-c> :call clearmatches()<CR>:noh<CR>

" -----------------------------------------------------------
" => h: window-resize
" -----------------------------------------------------------
" nnoremap <Leader>z :wincmd _<CR>:wincmd \|<CR>
nnoremap <silent><Leader>- :wincmd =<CR>

" http://stackoverflow.com/questions/1262154/minimizing-vertical-vim-window-splits
" z{nr}<CR>  Set current window height to {nr}. 
set winminheight=0
nmap <Leader>k <C-W>j<C-W>_
nmap <Leader>j <C-W>k<C-W>_

set winminwidth=0
nmap <Leader>l <C-W>h500<C-W>>
nmap <Leader>h <C-W>l500<C-W>>

nnoremap <M-Left> <C-w>>
nnoremap <M-Right> <C-w><
nnoremap <M-Up> <C-w>+
nnoremap <M-Down> <C-w>-

" -----------------------------------------------------------
" => Command mode mappings
" -----------------------------------------------------------
" refer to the directory of the current file, regardless of pwd
cnoremap %% <C-R>=expand('%:h').'/'<CR>

" -----------------------------------------------------------
" => Insert mode mappings
" -----------------------------------------------------------
" Start new line
inoremap <S-Return> <C-o>o

" insert absolute current buffer path
inoremap <F2> <C-R>=expand('%:p')<CR>

" quick movements
" http://vim.wikia.com/wiki/Quick_command_in_insert_mode
inoremap II <Esc>I
inoremap AA <Esc>A
" <C-\> does not eat last char of the line
inoremap CC <C-\><C-O>D
inoremap ,. <Esc>

" upper case
inoremap UU <Esc>gUiw`]a

imap     <Nul> <C-Space>
inoremap <C-Space> <C-x><C-l>

" -----------------------------------------------------------
" => vimgrep
" -----------------------------------------------------------
nnoremap <Leader>* [I
" find current word in quickfix
nnoremap <Leader>** :execute "vimgrep ".expand("<cword>")." %"<CR>:copen<CR><C-w>W
" find last search in quickfix
nnoremap <Leader>/ :execute 'vimgrep /'.@/.'/g %'<CR>:copen<CR><C-w>W

" }}}
" ============================================================================
" AUTOCMD {{{
" ============================================================================

augroup vimrcEx
  autocmd!

  " Help in new tabs
  autocmd BufEnter *.txt call <sid>helptab()

  " highlight cursorline in active window
  autocmd WinEnter * setlocal cursorline
  autocmd WinLeave * setlocal nocursorline

  " Create directory if not exists
  autocmd BufWritePre,FileWritePre * silent! call mkdir(expand('<afile>:p:h'), 'p')

  " When editing a file, always jump to the last known cursor position.
  " Don't do it for commit messages, when the position is invalid, or when
  " inside an event handler (happens when dropping a file on gvim).
  " :h last-position-jump
  autocmd BufReadPost *
        \ if &ft != 'gitcommit' && line("'\"") > 0 && line("'\"") <= line("$") |
        \   exe "normal g`\"" |
        \ endif

  " Automatically wrap at 80 characters for Markdown
  autocmd BufRead,BufNewFile *.md setlocal textwidth=80

  " Automatically wrap at 72 characters and spell check git commit messages
  autocmd FileType gitcommit setlocal textwidth=72

augroup END

augroup tmux_auto_rename
  autocmd!
  if exists('$TMUX') && !exists('$NORENAME')
    autocmd BufEnter * if empty(&buftype) | call system('tmux rename-window '.expand('%:t:S')) | endif
    autocmd VimLeave * call system('tmux set-window automatic-rename on')
  endif
augroup END

" }}}
" ============================================================================
" 'tpope/vim-fugitive' {{{
" ============================================================================

nnoremap <silent> <leader>gg :Gstatus<CR>
nnoremap <silent> <leader>gd :Gdiff<CR>
nnoremap <silent> <leader>gcc :Gcommit<CR>
nnoremap <silent> <leader>gb :Gblame<CR>
nnoremap <silent> <leader>gl :Glog<CR>
nnoremap <silent> <leader>gv :Gvsplit<CR>
nnoremap <silent> <leader>gp :Git push<CR>
" Git add %
nnoremap <silent> <leader>gw :Gwrite<CR>
" Git rm %
nnoremap <silent> <leader>gx :Gremove<CR>
" Git checkout %
nnoremap <silent> <leader>gr :Gread<CR>

" }}}
" ============================================================================
" 'junegunn/gv.vim' {{{
" ============================================================================

" Commits All
nnoremap <silent> <leader>gca :GV<CR>
" Commitns Only of the current file
nnoremap <silent> <leader>gco :GV!<CR>
" Commits Revisions of the current file
nnoremap <silent> <leader>gcr :GV?<CR>

" }}}
